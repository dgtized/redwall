<assembly-file> = line+
<line> = instruction? comment? <newline>
<newline> = #'\n|$'
comment = <';'> #'[^\n]*'
instruction = label-ref? operation args
label-ref = label* <':'?>
label = #'[a-zA-Z_][a-zA-Z0-9_]*'
operation = opcode | opcode <'.'> modifier
<opcode> =
        'DAT' | 'MOV' | 'ADD' | 'SUB' | 'MUL' | 'DIV' | 'MOD' |
        'JMP' | 'JMZ' | 'JMN' | 'DJN' | 'CMP' | 'SEQ' | 'SLT' | 'SNE'
        'SPL' | 'ORG' | 'EQU' | 'END'
<modifier> = 'A' | 'B' | 'AB' | 'BA' | 'F' | 'X' | 'I'
<args> = address? | address <','> address
address = mode? expr
<mode> = '#' | '$' | '@' | '<' | '>'
expr =
        term |
        term '+' expr | term '-' expr |
        term '*' expr | term '/' expr |
        term '%' expr
term = label | number | group
group = <'('> expr <')'>
number = whole-number | signed-integer
signed-integer = '+' whole-number | '-' whole-number
<whole-number> = #'[0-9]+'
